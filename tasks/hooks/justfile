set fallback := true
set shell := ["bash", "-uc"]
set ignore-comments

[no-cd]
help:
  @just -f tasks/hooks/justfile --list

[no-cd]
_default: help

[no-cd]
commit-msg message:
  pnpm exec commitlint --edit {{message}}

[no-cd]
pre-commit:
  pnpm exec lint-staged

[no-cd]
lint-staged *files:
  just hooks _ensure-newlines {{files}}

[no-cd]
_ensure-newlines *files:
  #!/usr/bin/env bash
  blocklist=("png" "jpg" "jpeg" "gif" "ico" "svg" "woff" "woff2" "ttf" "eot" "mp4" "mp3" "wav" "pdf" "zip" "tar" "gz")
  for file in {{files}}; do
    if [[ -f "$file" ]]; then
      ext="${file##*.}"
      ext="${ext,,}"
      blocked=false
      for blocked_ext in "${blocklist[@]}"; do
        if [[ "$ext" == "$blocked_ext" ]]; then
          blocked=true
          break
        fi
      done
      if [[ "$blocked" == false ]] && [[ -n "$(tail -c1 "$file" 2>/dev/null)" ]]; then
        echo "" >> "$file"
      fi
    fi
  done
